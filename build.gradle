plugins {
	id "fabric-loom" version "0.12-SNAPSHOT"
	id "maven-publish"
}

// Targeting Java 1.8 because we support Minecraft 1.16 (for now)
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

remapJar {
    archiveFileName = "${archivesBaseName}-${archiveVersion.get()}_${project.minecraft_version_least}-${project.minecraft_version}.${archiveExtension.get()}"
}

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url "https://maven.nucleoid.xyz/" }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs 'lib'
    }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.terraformersmc:modmenu:4.0.0-beta.2"
	modImplementation "com.ptsmods:devlogin:2.0"
	modImplementation "me.lucko:fabric-permissions-api:0.1-SNAPSHOT"
	modImplementation "com.github.astei:lazydfu:0.1.2"
    modImplementation "curse.maven:luckperms-431733:3545767"

	implementation include("com.github.Chocohead:Fabric-ASM:v2.3")
	implementation include(name: "discord-rpc-1.6.2")

    implementation include(project(path: 'api', configuration: 'namedElements'))
    implementation include(project(path: 'compat:compat16', configuration: 'namedElements'))
    implementation include(project(path: 'compat:compat17', configuration: 'namedElements'))
    implementation include(project(path: 'compat:compat18', configuration: 'namedElements'))
    implementation include(project(path: 'compat:compat182', configuration: 'namedElements'))
    implementation include(project(path: 'compat:compat19', configuration: 'namedElements'))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
	withSourcesJar()
}
